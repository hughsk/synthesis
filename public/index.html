<!DOCTYPE html>
<html>
    <head>
        <meta charset="utf-8">
        <title>Flora</title>
        <meta name="description" content="">
        <meta name="viewport" content="width=device-width">
        <style type="text/css">
            body, html {
                margin:0; padding:0;
                background:#fff;
            }
        </style>
    </head>
    <body>
        <script type="x-shader/x-vertex" id="petal-vertex">
            varying vec3 color;

uniform float curveHeightStart;
uniform float curveHeightEnd;
uniform float curveHeightScale;

uniform float curveWidthStart;
uniform float curveWidthEnd;
uniform float curveWidthScale;
    
uniform float growth;
uniform float twirl;

attribute float xpos;
attribute float widths;
    
varying float widthPoint;
varying float lengthPoint;

void main()
{
    float xpnt = xpos * growth;

    float twirlAngle = xpnt * twirl;
    float twirlSin = sin(twirlAngle);
    float twirlCos = cos(twirlAngle);

    mat4 twirler = mat4(
              1.0, 0.0, 0.0, 0.0,
              0.0, twirlCos, -twirlSin, 0.0,
              0.0, twirlSin,  twirlCos, 0.0,
              0.0, 0.0,            0.0, 1.0 
    );

    color = vec3(xpos, xpos, xpos);
    
    widthPoint = widths;
    lengthPoint = xpos;

    // Amplitude of the petal's width/height.
    // 
    // Note that the height is scaled by growth, but the
    // width remains the same.
    float ampHeight = sin(
        curveHeightStart + xpnt * (curveHeightEnd - curveHeightStart)
    ) * curveHeightScale;

    float ampWidth = widths * sin(
        curveWidthStart + xpos * (curveWidthEnd - curveWidthStart)
    ) * curveWidthScale;

    vec4 pos =  vec4(growth * position, 1.0) + vec4(0.0, ampHeight, ampWidth, 0.0);

    gl_Position = projectionMatrix * modelViewMatrix * twirler * pos;
}
        </script>
        <script type="x-shader/x-vertex" id="petal-fragment">
            uniform float lightness;
uniform float redness;
uniform float greeness;
uniform float blueness;
    
uniform float lineDarkness;
uniform float lines;
uniform float dirtiness;
uniform float border;
uniform float borderSize;

uniform float growth;
    
varying vec3 color;
varying float widthPoint;
varying float lengthPoint;

    
const float PI = 3.14159265359;

//
// Description : Array and textureless GLSL 2D simplex noise function.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : ijm
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
// 

vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec2 mod289(vec2 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec3 permute(vec3 x) {
  return mod289(((x*34.0)+1.0)*x);
}

float snoise(vec2 v) {
  const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626,  // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
// First corner
  vec2 i  = floor(v + dot(v, C.yy) );
  vec2 x0 = v -   i + dot(i, C.xx);

// Other corners
  vec2 i1;
  //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
  //i1.y = 1.0 - i1.x;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  // x0 = x0 - 0.0 + 0.0 * C.xx ;
  // x1 = x0 - i1 + 1.0 * C.xx ;
  // x2 = x0 - 1.0 + 2.0 * C.xx ;
  vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;

// Permutations
  i = mod289(i); // Avoid truncation effects in permutation
  vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
        + i.x + vec3(0.0, i1.x, 1.0 ));

  vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;

// Gradients: 41 points uniformly over a line, mapped onto a diamond.
// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

  vec3 x = 2.0 * fract(p * C.www) - 1.0;
  vec3 h = abs(x) - 0.5;
  vec3 ox = floor(x + 0.5);
  vec3 a0 = x - ox;

// Normalise gradients implicitly by scaling m
// Approximation of: m *= inversesqrt( a0*a0 + h*h );
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

// Compute final noise value at P
  vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}

//
//
// Actual Fragment Shader Code:
//
void main()
{
    float r = color[0] + (1.0 - color[0]) * lightness;
    float g = color[1] + (1.0 - color[1]) * lightness;
    float b = color[2] + (1.0 - color[2]) * lightness;
    
    r *= redness;
    g *= greeness;
    b *= blueness;
    
    vec3 c = vec3(r, g, b);
    
    if (lineDarkness != 0.0 && lines > 0.0) {
        // Bright points
        float line = 1. - abs(cos(lengthPoint * PI));
        // Darkening
        line *= lineDarkness;
        // Adding the lines
        line *= sin(widthPoint * lines);
        // Baseline
        line += 1.1;
        
        if (border > 0.0 && (widthPoint < borderSize * 2. - 1. || widthPoint > 1. - borderSize * 2.)) {
            c *= 1.0;
        } else
        if (line < 1.) {
            c *= line;
        }
    }

    if (border > 0.0) {
        if (widthPoint < borderSize - 1. || widthPoint > 1. - borderSize) {
            c *= (1. - border);
        }
    }

    if (dirtiness > 0.0) {
        c *= 1.0 + dirtiness * (snoise(
            vec2(widthPoint * 4., growth * lengthPoint * 18.)
        ) + snoise(
            vec2(widthPoint * 12., growth * lengthPoint * 40.)
        ));
    }
    
    gl_FragColor = vec4(c, 1.0);
}
        </script>
        <script type="text/javascript">
            window.isStatic = true;
        </script>
        <script src="vendor/dat.gui.js"></script>
        <script src="vendor/three.min.js"></script>
        <script src="index.js"></script>
    </body>
</html>